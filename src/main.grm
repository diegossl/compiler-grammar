"Case Sensitive" = True
"Start Symbol"   = <Program>

{id head} = {letter} + [_]
{id tail} = {id head} + {digit}
{string}  = {printable} - ["]
                 
id        = {id head} {id tail}*           
num       = {digit}+ | {digit}+'.'{digit}+          
str       = '"' ( {string} | '\'{printable} )* '"'
log       = true | false

Comment Start = '/*'
Comment End   = '*/'
Comment Line  = '//'

!-----------------------------------------------------------------------
!Start

    <Program>     ::= <Structs> <Const Block> <Var Block> <Start Block> <Decls> !first = {struct, const}                  

    <Structs>     ::= <Struct Block> <Structs>                                  !first = {struct}
                    |
               
    <Start Block> ::= procedure start '(' ')' <Proc Block>                      !first = {procedure}

    
    <Decls>       ::= <Decl> <Decls>                                            !first = {function, procedure}
                    |
              
    <Decl>        ::= <Func Decl>                                               !first = {function}
                    | <Proc Decl>                                               !first = {procedure}

!-----------------------------------------------------------------------
!Struct

    <Struct Block> ::= struct id <Extends> '{' <Const Block> <Var Block> '}'    !first = {struct}
    
    <Extends>      ::= extends struct id                                        !first = {extends}
                     |    

!-----------------------------------------------------------------------
!Constants and Variables Block

    <Const Block> ::= const '{' <Const Decls> '}'                               !first = {const}                  
                    |

    <Var Block>   ::= var '{' <Var Decls> '}'                                   !first = {var}
                    |

!-----------------------------------------------------------------------
!Variables Type and Typedef

    <Type>    ::= int                                                           !first = {int}
                | real                                                          !first = {real}
                | boolean                                                       !first = {boolean}
                | string                                                        !first = {string}
                | struct id                                                     !first = {struct}                      

    <Typedef> ::= typedef <Type> id ';'                                         !first = {typedef}

!-----------------------------------------------------------------------
!Variable Declarations

    <Var Decls> ::= <Var Decl> <Var Decls>                                      !first = {int, real, boolean, string, struct, typedef, id, local, global}
                  | 

    <Var Decl>  ::= <Type> <Var> <Var List> ';'                                 !first = {int, real, boolean, string, struct}
                  | <Typedef>                                                   !first = {typedef}
                  | <Stm Scope>                                                 !first = {local, global}
                  | id <Var Id>                                                 !first = {id}
                  
    <Var Id>    ::= <Var> <Var List> ';'                                        !first = {id}
                  | <Stm Id>                                                    !first = {'=', '[', '.' '('}

    <Var>       ::= id <Arrays>                                                 !first = {id}

    <Var List>  ::= ',' <Var> <Var List>                                        !first = {','}
                  |                                                              
    
!-----------------------------------------------------------------------
!Constant Declarations

    <Const Decls> ::= <Const Decl> <Const Decls>                                !first = {int, real, boolean, string, struct, typedef, id, local, global}
                    | 

    <Const Decl>  ::= <Type> <Const> <Const List>                               !first = {int, real, boolean, string, struct}
                    | <Typedef>                                                 !first = {typedef}
                    | <Stm Scope>                                               !first = {local, global}
                    | id <Const Id>                                             !first = {id}
                  
    <Const Id>    ::= <Const> <Const List> ';'                                  !first = {id}
                    | <Stm Id>                                                  !first = {'=', '[', '.' '('}

    <Const>       ::= id <Arrays>                                               !first = {id}

    <Const List>  ::= ',' <Const> <Const List>                                  !first = {','}
                    |  '=' <Decl Atribute>';'


    <Decl Atribute> ::= <Array Decl>                                            !first = {'{'}
                      |  <Expr>                                                 !first = {'!', num, str, log, id '('}


!-----------------------------------------------------------------------
!Arrays Atributes
<Array Decl>    ::= '{' <Array Def> '}' <Array Vector>                          !first = {'{'}
<Array Vector> ::= ',' <Array Decl> |
                
<Array Def>     ::= <Expr> <Array Expr>                                         !first = {'!', num, str, log, id '('}
<Array Expr> ::= ',' <Array Def> |
 
!-----------------------------------------------------------------------
!Arrays

    <Array>  ::= '[' <Index> ']'                                                !first = {'['}

    <Index>  ::= <Expr>                                                         !first = {'!', num, str, log, id '('}
               |                                                                

    <Arrays> ::= <Array> <Arrays>                                               !first = {'['}
               |

!-----------------------------------------------------------------------
!Assign and Access

    <Assign>   ::= '=' <Expr> ';'                                               !first = {'='}
                 | '++' ';'                                                     !first = {'++'}
                 | '--' ';'                                                     !first = {'--'}

    <Access>   ::= '.' id <Arrays>                                              !first = {'.'}

    <Accesses> ::= <Access> <Accesses>                                          !first = {'.'}
                 |

!-----------------------------------------------------------------------
!Arguments

    <Args>      ::= <Expr> <Args List>                                          !first = {'!', num, str, log, id '('}
                  |

    <Args List> ::= ',' <Expr> <Args List>                                      !first = {','}
                  |

!-----------------------------------------------------------------------
!Function Declaration

    <Func Decl> ::= function <Param Type> id '(' <Params> ')' <Func Block>      !first{function}

!-----------------------------------------------------------------------
!Procedure Declaration

    <Proc Decl> ::= procedure id '(' <Params> ')' <Proc Block>                  !first{procedure}

!-----------------------------------------------------------------------
!Parameters

    <Param Type>   ::= <Type>                                                   !first = {int, real, boolean, string, struct}
                     | id                                                       !first = {id}

    <Params>       ::= <Param> <Params List>                                    !first = {int, real, boolean, string, struct, id}
                     |

    <Param>        ::= <Param Type> id <Param Arrays>                           !first = {int, real, boolean, string, struct, id}

    <Params List>  ::= ',' <Param> <Params List>                                !first = {','}
                     |

    <Param Arrays> ::= '[' ']' <Param Mult Arrays>                              !first = {'['}
                     |

    <Param Mult Arrays> ::= '[' num ']' <Param Mult Arrays>                     !first = {'['}
                          |

!-----------------------------------------------------------------------
!Function Block

    <Func Block>      ::= '{' <Var Block> <Func Stms> return ';' '}'                                 !first = {'{'}

    <Func Stms>       ::= <Func Stm> <Func Stms>                                          !first = {if, while, '{', id, local, global, print, read, ';', return}
                        |
                   
    <Func Stm>        ::= if '(' <Expr> ')' then <Func If Stm>                            !first = {if}
                        | while '(' <Expr> ')' <Func Stm>                                 !first = {while}
                        | <Func Normal Stm>                                               !first = {'{', id, local, global, print, read, ';', return}
                        
    <Func If Stm>     ::= <Func Stm>                                                      !first = {if, while, '{', id, local, global, print, read, ';', return}
                        | <Func Then Stm> else <Func Stm>                                 !first = {if, while, '{', id, local, global, print, read, ';', return}
                   
    <Func Then Stm>   ::= if '(' <Expr> ')' then <Func Then Stm> else <Func Then Stm>     !first = {if}
                        | while '(' <Expr> ')' <Func Then Stm>                            !first = {while}
                        | <Func Normal Stm>                                               !first = {'{', id, local, global, print, read, ';', return}
                   
    <Func Normal Stm> ::= '{' <Func Stms> '}'                                             !first = {'{'}
                        | <Var Stm>                                                       !first = {id, local, global, print, read}
                        | ';'                                                             !first = {';'}
                        | return <Expr> ';'                                               !first = {return}

!-----------------------------------------------------------------------
!Procedure Block

    <Proc Block>      ::= '{' <Var Block> <Proc Stms> '}'                                 !first = {'{'}

    <Proc Stms>       ::= <Proc Stm> <Proc Stms>                                          !first = {if, while, '{', id, local, global, print, read, ';'}
                        |
                   
    <Proc Stm>        ::= if '(' <Expr> ')' then <Proc If Stm>                            !first = {if}
                        | while '(' <Expr> ')' <Proc Stm>                                 !first = {while}
                        | <Proc Normal Stm>                                               !first = {'{', id, local, global, print, read, ';'}
                        
    <Proc If Stm>     ::= <Proc Stm>                                                      !first = {if, while, '{', id, local, global, print, read, ';'}
                        | <Proc Then Stm> else <Proc Stm>                                 !first = {if, while, '{', id, local, global, print, read, ';'}
                   
    <Proc Then Stm>   ::= if '(' <Expr> ')' then <Proc Then Stm> else <Proc Then Stm>     !first = {if}
                        | while '(' <Expr> ')' <Proc Then Stm>                            !first = {while}
                        | <Proc Normal Stm>                                               !first = {'{', id, local, global, print, read, ';'}
                   
    <Proc Normal Stm> ::= '{' <Proc Stms> '}'                                             !first = {'{'}
                        | <Var Stm>                                                       !first = {id, local, global, print, read}
                        | ';'                                                             !first = {';'}
                      
!-----------------------------------------------------------------------
!Variable Statements

    <Var Stm>   ::= <Stm Scope>                                                           !first = {local, global}
                  | id <Stm Id>                                                           !first = {id}
                  | <Stm Cmd>                                                             !first = {print, read}

    <Stm Id>    ::= <Assign>                                                              !first = {'=', '+', '-'}
                  | <Array> <Arrays> <Accesses> <Assign>                                  !first = {'['}
                  | <Access> <Accesses> <Assign>                                          !first = {'.'}
                  | '(' <Args> ')' ';'                                                    !first = {'('}

    <Stm Scope> ::= local <Access> <Accesses> <Assign>                                    !first = {local}
                  | global <Access> <Accesses> <Assign>                                   !first = {global}

    <Stm Cmd>   ::= print '(' <Args> ')' ';'                                              !first = {print}
                  | read '(' <Args> ')' ';'                                               !first = {read}

!-----------------------------------------------------------------------
!Expressions  
    
    <Expr>     ::= <Or>                                                                   !first = {'!', num, str, log, id '('}
                
    <Or>       ::= <And> <Or_>                                                            !first = {'!', num, str, log, id '('}
    <Or_>      ::= '||' <And> <Or_>                                                       !first = {'||'}
                 | 

    <And>      ::= <Equate> <And_>                                                        !first = {'!', num, str, log, id '('}
    <And_>     ::= '&&' <Equate> <And_>                                                   !first = {'&&'}
                 | 

    <Equate>   ::= <Compare> <Equate_>                                                    !first = {'!', num, str, log, id '('}
    <Equate_>  ::= '==' <Compare> <Equate_>                                               !first = {'=='}
                 | '!=' <Compare> <Equate_>                                               !first = {'!='}
                 | 

    <Compare>  ::= <Add> <Compare_>                                                       !first = {'!', num, str, log, id '('}
    <Compare_> ::= '<'  <Add> <Compare_>                                                  !first = {'>'}
                 | '>' <Add> <Compare_>                                                   !first = {'<'}
                 | '<=' <Add> <Compare_>                                                  !first = {'<='}
                 | '>=' <Add> <Compare_>                                                  !first = {'>='}
                 | 

    <Add>      ::= <Mult> <Add_>                                                          !first = {'!', num, str, log, id '('}
    <Add_>     ::= '+' <Mult> <Add_>                                                      !first = {'+'}
                 | '-' <Mult> <Add_>                                                      !first = {'-'}
                 | 
                   
    <Mult>     ::= <Unary> <Mult_>                                                        !first = {'!', num, str, log, id '('}
    <Mult_>    ::= '*' <Unary> <Mult_>                                                    !first = {'*'}
                 | '/' <Unary> <Mult_>                                                    !first = {'/'}
                 | 

    <Unary>    ::= '!' <Unary>                                                            !first = {'!'}
                 | <Value>                                                                !first = {num, str, log, id '('}

    <Value>    ::= num                                                                    !first = {num}
                 | str                                                                    !first = {str}
                 | log                                                                    !first = {log}
                 | local <Access>                                                         !first = {local}
                 | global <Access>                                                        !first = {global}
                 | id <Id Value>                                                          !first = {id}
                 | '(' <Expr> ')'                                                         !first = {'('}

    <Id Value> ::= <Arrays> <Accesses>                                                    !first = {'[', '.'}
                 | '(' <Args> ')'                                                         !first = {'('}

